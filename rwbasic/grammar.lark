// Common base tokens
%import common (DIGIT, HEXDIGIT, NEWLINE, LETTER)

// Ignore whitespace
%import common (WS_INLINE)
%ignore WS_INLINE

// Numeric literal tokens
BOOLEAN_LITERAL: "TRUE"i | "FALSE"i
BINARY_LITERAL: "%" ("0".."1")+
DECIMAL_LITERAL: DIGIT+
HEX_LITERAL: "&" HEXDIGIT+
_EXPONENT: "E"i ["+" | "-"] DIGIT+
_FIXED_LITERAL: DECIMAL_LITERAL "." DECIMAL_LITERAL?
FLOAT_LITERAL: DECIMAL_LITERAL _EXPONENT | _FIXED_LITERAL _EXPONENT?

// String literal
STRING_LITERAL: "\"" (/[^"]/ | "\"\"")* "\""

// Variable reference. Low priority so we prefer matching terminals like TRUE and FALSE first.
VARLETTER: LETTER | "_" | "@"
VARIABLE.-1: VARLETTER (VARLETTER | DIGIT)* ("$" | "%")?

// Punctuation
EQUALS: "="
SEMICOLON: ";"
APOSTROPHE: "'"
COMMA: ","

// Operators
POWEROP: "^"
MULOP: "*" | "/"
ADDOP: "+" | "-"
COMPOP: "<>" | "<" | ">" | "<=" | ">=" | "<<" | ">>" | ">>>"

// Keywords.
// TODO: abbreviations, see e.g. https://central.kaserver5.org/Kasoft/Typeset/BBC/Ch47.html
AND: "AND"i
CASE: "CASE"i
_DEF: "DEF"i
DIV: "DIV"i
ELSE: "ELSE"i
END: "END"i
ENDCASE: "ENDCASE"i
ENDIF: "ENDIF"i
ENDPROC: "ENDPROC"i
ENDWHILE: "ENDWHILE"i
EOR: "EOR"i
FOR: "FOR"i
IF: "IF"i
LET: "LET"i
LIST: "LIST"i
LOCAL: "LOCAL"i
MOD: "MOD"i
NEW: "NEW"i
NEXT: "NEXT"i
NOT: "NOT"i
OF: "OF"i
OR: "OR"i
OTHERWISE: "OTHERWISE"i
PRINT: "PRINT"i
RENUMBER: "RENUMBER"i
REPEAT: "REPEAT"i
RUN: "RUN"i
STEP: "STEP"i
THEN: "THEN"i
TO: "TO"i
UNTIL: "UNTIL"i
WHEN: "WHEN"i
WHILE: "WHILE"i

PROCEDURE_NAME: "PROC"i VARLETTER (VARLETTER | DIGIT)*
FUNCTION_NAME: "FN"i VARLETTER (VARLETTER | DIGIT)*

// #!-style lines on Unix
shebang_line: /#![^\n]*/

// A program definition
program: (shebang_line NEWLINE)? (program_line NEWLINE)* program_line
?program_line: numbered_line_definition | unnumbered_line_definition
unnumbered_line_definition: line_definition_statements
numbered_line_definition: DECIMAL_LITERAL line_definition_statements

// Input to the interactive prompt
?promptline: numbered_line_update
           | prompt_line_statements
numbered_line_update: DECIMAL_LITERAL line_definition_statements
prompt_line_statements: (prompt_line_statement ":")* \
                            (prompt_line_statement | prompt_line_final_statement)?

line_definition_statements: (line_definition_initial_statement ":")? \
                                (line_definition_statement ":")* \
                                (line_definition_statement | line_definition_final_statement)?
                          | line_definition_initial_statement

// Statements which can appear anywhere in line definitions.
?line_definition_statement: statement
                          | end_statement
                          | for_statement
                          | next_statement
                          | repeat_statement
                          | until_statement
                          | while_statement
                          | endwhile_statement
                          | defproc_statement
                          | endproc_statement
                          | deffn_statement
                          | endfn_statement

// Statements which can appear in line definitions but only at the start
?line_definition_initial_statement: else_statement
                                  | endif_statement
                                  | endcase_statement
                                  | when_statement
                                  | otherwise_statement

// Statements which can appear in line definitions but only at the end
?line_definition_final_statement: if_statement
                                | comment_statement
                                | case_statement

// Statements which can appear in the prompt line.
?prompt_line_statement: statement
                      | new_statement
                      | run_statement
                      | list_statement
                      | renumber_statement
// Statements which can appear at the end of the prompt line.
?prompt_line_final_statement: comment_statement

// Statements within a line which can appear in line definitions or the prompt line.
?statement: print_statement
          | let_statement
          | inline_for_statement
          | inline_if_statement
          | inline_repeat_statement
          | inline_while_statement
          | proc_call_statement
          | local_statement

// Prompt-only statements
run_statement: RUN
new_statement: NEW
list_statement: LIST
renumber_statement: RENUMBER

// Program-only statements
end_statement: END

// Prompt and program statements
comment_statement: /REM[^\n]*/i
print_statement: PRINT (expression+ (COMMA | SEMICOLON | APOSTROPHE))* expression*
let_statement: LET? VARIABLE EQUALS expression
for_statement: FOR VARIABLE EQUALS expression TO expression (STEP expression)?
next_statement: NEXT VARIABLE?
if_statement: IF expression THEN
else_statement: ELSE
endif_statement: ENDIF
repeat_statement: REPEAT
until_statement: UNTIL expression
while_statement: WHILE expression
endwhile_statement: ENDWHILE
case_statement: CASE expression OF
when_statement: WHEN (expression ",")* expression
endcase_statement: ENDCASE
otherwise_statement: OTHERWISE
defproc_statement: _DEF PROCEDURE_NAME ("(" ((VARIABLE ",")* VARIABLE)? ")")?
endproc_statement: ENDPROC
proc_call_statement: PROCEDURE_NAME ("(" ((expression ",")* expression)? ")")?
local_statement: LOCAL (VARIABLE ",")* VARIABLE
deffn_statement: _DEF FUNCTION_NAME ("(" ((VARIABLE ",")* VARIABLE)? ")")?
endfn_statement: "=" expression

// Inline loops, can appear in prompt lines or programs but covers only one line.
inline_for_statement: for_statement ":" (statement ":")* next_statement
inline_repeat_statement: repeat_statement ":" (statement ":")* until_statement
inline_while_statement: while_statement ":" (statement ":")* endwhile_statement

// Inline IF statement, can appear in prompt lines or programs but covers only one line.
inline_if_statement: inline_if_header inline_statement_block (ELSE inline_statement_block)?
inline_if_header: IF expression THEN?
inline_statement_block: (statement ":")* statement

// Expressions
?andop: AND
?orop: OR | EOR
?unaryop: ADDOP | NOT // TODO indirection
?mulop: MULOP | DIV | MOD
?compop: EQUALS | COMPOP

?expression: orexpr
?orexpr: (andexpr orop)* andexpr
?andexpr: (compexpr andop)* compexpr
?compexpr: (addexpr compop)* addexpr
?addexpr: (mulexpr ADDOP)* mulexpr
?mulexpr: (powerexpr mulop)* powerexpr
?powerexpr: (unaryexpr POWEROP)* unaryexpr
?unaryexpr: unaryop* atomexpr
?atomexpr: "(" expression ")"
         | numliteralexpr
         | strliteralexpr
         | variablerefexpr
         | funcorarrayexpr
numliteralexpr: BOOLEAN_LITERAL | BINARY_LITERAL | DECIMAL_LITERAL | HEX_LITERAL | FLOAT_LITERAL
strliteralexpr: STRING_LITERAL
funcorarrayexpr: (VARIABLE | FUNCTION_NAME) "(" ( (expression ",")* expression )? ")"
variablerefexpr: VARIABLE
