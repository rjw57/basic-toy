// Common base tokens
%import common (DIGIT, HEXDIGIT, NEWLINE)

// Ignore whitespace
%import common (WS_INLINE)
%ignore WS_INLINE

// Comment body. Will match to end of line including any ":" characters.
COMMENT_BODY: /[^\r\n]/+

// Numeric literal tokens
BOOLEAN_LITERAL: "TRUE"i | "FALSE"i
BINARY_LITERAL: "%" ("0".."1")+
DECIMAL_LITERAL: DIGIT+
HEX_LITERAL: "&" HEXDIGIT+
_EXPONENT: "E"i ["+" | "-"] DIGIT+
_FIXED_LITERAL: DECIMAL_LITERAL "." DECIMAL_LITERAL?
FLOAT_LITERAL: DECIMAL_LITERAL _EXPONENT | _FIXED_LITERAL _EXPONENT?

// String literal
STRING_LITERAL: "\"" (/[^"]/ | "\"\"")* "\""

// Classes of operator tokens grouped by priority. Operators listed first are acted upon before
// operators listed second and so on.
UNARYOP: "+" | "-" | "NOT"i // TODO: indirection
POWEROP: "^"
MULOP: "*" | "/" | "DIV"i | "MOD"i
ADDOP: "+" | "-"
COMPOP: "=" | "<>" | "<" | ">" | "<=" | ">=" | "<<" | ">>" | ">>>"
ANDOP: "AND"i
OROP: "OR"i | "EOR"i

// Symbols
SEMICOLON: ";"

// Keywords
PRINT: "PRINT"i
REM: "REM"i

// Usually we start with a program
?start: program

// A program definition
program: (line_definition NEWLINE)* line_definition NEWLINE?

// Input to the interactive prompt
?promptline: line_definition | line_statements

// Define a line via a line number.
line_definition: DECIMAL_LITERAL line_statements

// Statements within a line.
line_statements: (statement ":")* statement
?statement: comment_statement
          | print_statement
comment_statement: REM COMMENT_BODY?
print_statement: PRINT expression SEMICOLON?

// Expressions
?expression: orexpr
?orexpr: (andexpr OROP)* andexpr
?andexpr: (compexpr ANDOP)* compexpr
?compexpr: (addexpr COMPOP)* addexpr
?addexpr: (mulexpr ADDOP)* mulexpr
?mulexpr: (powerexpr MULOP)* powerexpr
?powerexpr: (unaryexpr POWEROP)* unaryexpr
?unaryexpr: UNARYOP* atomexpr
?atomexpr: "(" expression ")"
         | literalexpr

literalexpr: BOOLEAN_LITERAL | BINARY_LITERAL | DECIMAL_LITERAL | HEX_LITERAL | FLOAT_LITERAL
           | STRING_LITERAL
